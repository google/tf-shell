module(
    name = "tf-shell",
    version = "0.1.4",
)

SUPPORTED_PYTHON_VERSIONS = [
    "3.9",
    "3.10",
    "3.11",
    "3.12",
]  # Also see ./.github/workflows/wheel.yaml

DEFAULT_PYTHON = "3.10"  # Also see ./BUILD

bazel_dep(name = "rules_python", version = "0.33.2")

python = use_extension("@rules_python//python/extensions:python.bzl", "python")

[
    python.toolchain(
        is_default = version == DEFAULT_PYTHON,
        python_version = version,
    )
    for version in SUPPORTED_PYTHON_VERSIONS
]

use_repo(
    python,
    "python_versions",
)

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")

[
    pip.parse(
        hub_name = "pip",
        python_version = version,
        requirements_lock = "//:requirements_" + version.replace(".", "_") + ".txt",
    )
    for version in SUPPORTED_PYTHON_VERSIONS
]

use_repo(pip, "pip")

# See what version of abseil tensorflow is using here:
# https://github.com/tensorflow/tensorflow/blob/master/third_party/absl/workspace.bzl
bazel_dep(name = "abseil-cpp", version = "20230802.1", repo_name = "com_google_absl")
bazel_dep(name = "rules_cc", version = "0.0.9")
bazel_dep(name = "glog", version = "0.7.1", repo_name = "com_github_google_glog")
bazel_dep(name = "boringssl", version = "0.0.0-20240530-2db0eb3")
bazel_dep(name = "googletest", version = "1.14.0")
bazel_dep(name = "tink_cc", version = "2.1.3", repo_name = "com_github_tink_crypto_tink_cc")

bazel_dep(name = "buildifier_prebuilt", version = "7.1.2", dev_dependency = True)
